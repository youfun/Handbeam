defmodule Handbeam.Platform.ProcessSandbox do
  @moduledoc """
  Builds an OS-enforced command boundary for workspace-scoped processes.

  Both backends keep the host filesystem readable and allow writes only to the
  workspace, a private temp area, and explicitly configured extra directories
  (`config :handbeam, :sandbox_writable_paths` or the `:writable_paths` option).
  Network access is not restricted by either backend.

  * Linux uses Bubblewrap with a read-only host filesystem, private PID/IPC/UTS
    namespaces, and a tmpfs over `/tmp` and `/var/tmp`.
  * macOS uses Seatbelt (`sandbox-exec`). There are no namespaces: the private
    temp area is a per-workspace directory exported as `TMPDIR`, host `/tmp`
    stays read-only, and signals may only target processes in the same sandbox.
  """

  alias Handbeam.Security.PathValidator

  @seatbelt_executable "/usr/bin/sandbox-exec"

  @type command :: %{
          executable: Path.t(),
          args: [String.t()],
          cwd: Path.t() | nil,
          env: [{String.t(), String.t()}],
          pid_namespace?: boolean()
        }

  @spec wrap(map(), String.t(), Path.t() | nil, keyword()) ::
          {:ok, command()} | {:error, String.t()}
  def wrap(shell, command, cwd, opts) do
    case Keyword.get(opts, :workspace_path) do
      nil ->
        {:ok,
         %{
           executable: shell.path,
           args: shell.args ++ [command],
           cwd: cwd,
           env: [],
           pid_namespace?: false
         }}

      workspace ->
        wrap_workspace(shell, command, cwd || workspace, workspace, opts)
    end
  end

  @doc """
  Normalizes extra writable directories: expands `~`, resolves symlinks, keeps
  existing directories only, and rejects `/` and the home directory itself.
  """
  @spec writable_paths([term()]) :: [Path.t()]
  def writable_paths(paths) when is_list(paths) do
    home = PathValidator.resolve_symlink(System.user_home!())

    paths
    |> Enum.filter(&is_binary/1)
    |> Enum.map(&PathValidator.resolve_symlink(Path.expand(&1)))
    |> Enum.filter(&File.dir?/1)
    |> Enum.reject(&(&1 in ["/", home]))
    |> Enum.uniq()
  end

  @doc """
  Seatbelt profile for `writable_count` extra directories. Paths are never
  interpolated; they arrive as `-D` parameters so they cannot alter the policy.
  """
  @spec seatbelt_profile(non_neg_integer()) :: String.t()
  def seatbelt_profile(writable_count) when is_integer(writable_count) and writable_count >= 0 do
    extra =
      if writable_count == 0,
        do: "",
        else:
          Enum.map_join(0..(writable_count - 1), "\n", &~s|  (subpath (param "WRITABLE_#{&1}"))|)

    """
    (version 1)
    (allow default)
    (deny signal)
    (allow signal (target same-sandbox))
    (deny file-write*)
    (allow file-write*
      (subpath (param "WORKSPACE"))
      (subpath (param "TMPDIR"))
    #{extra}
      (literal "/dev/null")
      (literal "/dev/zero")
      (literal "/dev/tty")
      (literal "/dev/dtracehelper")
      (regex #"^/dev/fd/[0-9]+$")
      (regex #"^/dev/ttys[0-9]+$"))
    """
  end

  @spec seatbelt_args(map(), String.t(), Path.t(), Path.t(), [Path.t()]) :: [String.t()]
  def seatbelt_args(shell, command, workspace, tmp_dir, writable) do
    params =
      writable
      |> Enum.with_index()
      |> Enum.flat_map(fn {path, index} -> ["-D", "WRITABLE_#{index}=#{path}"] end)

    ["-D", "WORKSPACE=#{workspace}", "-D", "TMPDIR=#{tmp_dir}"] ++
      params ++
      ["-p", seatbelt_profile(length(writable)), shell.path | shell.args ++ [command]]
  end

  defp wrap_workspace(shell, command, cwd, workspace, opts) do
    workspace = PathValidator.resolve_symlink(Path.expand(workspace))
    cwd = PathValidator.resolve_symlink(Path.expand(cwd))

    with :ok <- validate_directory(workspace, "workspace"),
         :ok <- validate_directory(cwd, "cwd"),
         :ok <- PathValidator.validate_within_workspace(cwd, workspace) do
      writable =
        writable_paths(
          Application.get_env(:handbeam, :sandbox_writable_paths, []) ++
            Keyword.get(opts, :writable_paths, [])
        )

      case :os.type() do
        {:unix, :linux} -> wrap_linux(shell, command, cwd, workspace, writable, opts)
        {:unix, :darwin} -> wrap_darwin(shell, command, cwd, workspace, writable, opts)
        _ -> {:error, unsupported()}
      end
    end
  end

  defp wrap_linux(shell, command, cwd, workspace, writable, opts) do
    case Keyword.get(opts, :sandbox_path) || System.find_executable("bwrap") do
      nil ->
        {:error,
         "Workspace-confined bash requires Bubblewrap (bwrap); refusing to run without an OS sandbox"}

      path ->
        with {:ok, sandbox} <- validate_executable(path) do
          {:ok,
           %{
             executable: sandbox,
             args: bwrap_args(shell, command, cwd, workspace, writable),
             cwd: nil,
             env: [],
             pid_namespace?: true
           }}
        end
    end
  end

  defp wrap_darwin(shell, command, cwd, workspace, writable, opts) do
    with {:ok, sandbox} <-
           validate_executable(Keyword.get(opts, :sandbox_path, @seatbelt_executable)),
         {:ok, tmp_dir} <- private_tmp_dir(workspace) do
      {:ok,
       %{
         executable: sandbox,
         args: seatbelt_args(shell, command, workspace, tmp_dir, writable),
         cwd: cwd,
         env: [{"TMPDIR", tmp_dir}],
         pid_namespace?: false
       }}
    end
  end

  # One directory per workspace, so no per-run lifecycle is needed; it sits
  # below the host temp root, never at it, so siblings there stay read-only.
  defp private_tmp_dir(workspace) do
    hash = :crypto.hash(:sha256, workspace) |> Base.url_encode64(padding: false)
    dir = Path.join([System.tmp_dir!(), "handbeam-sandbox", binary_part(hash, 0, 16)])

    case File.mkdir_p(dir) do
      :ok ->
        File.chmod(dir, 0o700)
        {:ok, PathValidator.resolve_symlink(dir)}

      {:error, reason} ->
        {:error, "Cannot create sandbox temp dir: #{inspect(reason)}"}
    end
  end

  defp unsupported,
    do:
      "Workspace-confined bash is not supported on this platform; refusing to run without an OS sandbox"

  defp validate_executable(path) do
    expanded = Path.expand(path)

    if File.regular?(expanded) do
      {:ok, expanded}
    else
      {:error, "Sandbox executable not found: #{path}"}
    end
  end

  defp validate_directory(path, label) do
    if File.dir?(path), do: :ok, else: {:error, "#{label} #{path} is not a directory"}
  end

  defp bwrap_args(shell, command, cwd, workspace, writable) do
    [
      "--die-with-parent",
      "--new-session",
      "--unshare-pid",
      "--unshare-ipc",
      "--unshare-uts",
      "--ro-bind",
      "/",
      "/",
      "--dev",
      "/dev",
      "--proc",
      "/proc",
      "--tmpfs",
      "/tmp",
      "--tmpfs",
      "/var/tmp",
      "--bind",
      workspace,
      workspace
    ] ++
      Enum.flat_map(writable, &["--bind", &1, &1]) ++
      [
        "--chdir",
        cwd,
        shell.path
        | shell.args ++ [command]
      ]
  end
end
